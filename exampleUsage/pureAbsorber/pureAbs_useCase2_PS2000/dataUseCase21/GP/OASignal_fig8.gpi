set terminal postscript enhanced eps color lw 1.5 "Times-Roman" 24
set output "./FIGS/oaSignals_fig8.eps"

LW = 1.5
set style line 1 lw LW lt 1 lc rgb '#332288'
set style line 2 lw LW lt 1 lc rgb '#88CCEE'
set style line 3 lw LW lt 1 lc rgb '#44AA99'
set style line 4 lw LW lt 1 lc rgb '#117733'
set style line 5 lw LW lt 1 lc rgb '#999933'
set style line 6 lw LW lt 1 lc rgb '#DDCC77'
set style line 7 lw LW lt 1 lc rgb '#CC6677'
set style line 8 lw LW lt 1 lc rgb '#882255'
set style line 9 lw LW lt 1 lc rgb '#AA4499'

set key samplen 1.0

set xrange [-0.01:0.13]
scaley(y)=y/10**6

set ylabel "{/Times-Italic p} (MPa)" 
set format y "%3g"
set xzeroaxis

set lmargin at screen 0.13
set rmargin at screen 0.9
set multiplot

set tmargin at screen 0.9
set bmargin at screen 0.55
set xtics ('' -0.05,'' 0, '' 0.025, '' 0.05, '' 0.075, '' 0.1, '' 0.13)
set label 11 '(a)' at screen 0.,0.9
set label 12 "{/Symbol l}=550 nm \n{/Symbol m}_{/=16 a}=13 cm^{-1}" at graph 0.4,0.9
p "../L550nm_ma13.0_ftd0.3.dat" u 2:(scaley($3)) w l ls 1 t '{/Times-Italic d}=0.3 cm'\
, "../L550nm_ma13.0_ftd0.06.dat" u 2:(scaley($3)) w l ls 9 t '0.06 cm'
unset label 11
unset label 12

set tmargin at screen 0.5
set bmargin at screen 0.15
set xtics (-0.05,0,0.025,0.05,0.075,0.1,'{/Times-Italic c}{/Symbol t} (cm)' 0.13)
set label 21 '(b)' at screen 0.,0.5
set label 22 "{/Symbol l}=532 nm \n{/Symbol m}_{/=16 a}=80 cm^{-1}" at graph 0.4,0.9
p "../L532nm_ma80.0_ftd0.3.dat" u 2:(scaley($3)) w l ls 1 t '{/Times-Italic d}=0.3 cm'\
, "../L532nm_ma80.0_ftd0.06.dat" u 2:(scaley($3)) w l ls 9 t '0.06 cm'
unset label 21
unset label 22

unset multiplot
