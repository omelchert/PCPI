set terminal postscript enhanced eps color lw 1.5 "Times-Roman" 22
set output "./FIGS/mcxyz_oaSig_zScan_yScan.eps"

LW = 1.5
set style line 1 lw LW lt 1 lc rgb '#332288' 
set style line 2 lw LW lt 1 lc rgb '#88CCEE'  
set style line 3 lw LW lt 1 lc rgb '#44AA99' 
set style line 4 lw LW lt 1 lc rgb '#117733' 
set style line 5 lw LW lt 1 lc rgb '#999933' 
set style line 6 lw LW lt 1 lc rgb '#DDCC77' 
set style line 7 lw LW lt 1 lc rgb '#CC6677' 
set style line 8 lw LW lt 1 lc rgb '#882255' 
set style line 9 lw LW lt 1 lc rgb '#AA4499' 

set key samplen 1.0
set key at graph 0.98,0.5 

set xrange [-0.002:0.06]

set ylabel "{/Times-Italic p} (kPa per J)" 
set yr [-2:1.5]
set ytics (-2,-1,0,1,2)
set xzeroaxis

scale(y)=y/1000.

set lmargin at screen 0.1
set rmargin at screen 0.94


set multiplot 
set label 1 "(b)" at screen 0.,0.5
set label 11 "{/Times-Italic x}_{/=16 D}=0.05 cm\n{/Times-Italic y}_{/=16 D}=0.05 cm" at graph 0.70,0.9
set bmargin at screen 0.1
set tmargin at screen 0.5
set xtics (0,0.01,0.02,0.03,0.04,0.05,'{/Times-Italic c}{/Symbol t} (cm)' 0.06)
p \
"../oaSig_zD-0.1.dat"  u 1:(scale($2)) w l ls 1 t '{/Times-Italic z}_{/=16 D}=-0.1 cm',\
"../oaSig_zD-0.2.dat"  u 1:(scale($2)) w l ls 3 t '-0.2 cm',\
"../oaSig_zD-0.4.dat"  u 1:(scale($2)) w l ls 5 t '-0.4 cm',\
"../oaSig_zD-1.6.dat"  u 1:(scale($2)) w l ls 7 t '-0.8 cm',\

unset label 1
unset label 11

set label 2 "(a)" at screen 0.,0.95
set label 21 "{/Times-Italic x}_{/=16 D}=0.05 cm\n{/Times-Italic z}_{/=16 D}=-0.2 cm" at graph 0.70,0.9
set bmargin at screen 0.55
set tmargin at screen 0.95
set xtics ('' 0,'' 0.01,'' 0.02,'' 0.03,'' 0.04,'' 0.05,'' 0.06)
p \
"../oaSig_yD0.05.dat"  u 1:(scale($2)) w l ls 1 t '{/Times-Italic y}_{/=16 D}=0.050 cm',\
"../oaSig_yD0.04.dat"  u 1:(scale($2)) w l ls 3 t '0.040 cm',\
"../oaSig_yD0.03.dat"  u 1:(scale($2)) w l ls 5 t '0.030 cm',\
"../oaSig_yD0.02.dat"  u 1:(scale($2)) w l ls 7 t '0.020 cm',\

unset multiplot
