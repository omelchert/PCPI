set terminal postscript enhanced eps color lw 1 "Times-Roman" 12
set output "./FIGS/illustrateISPs_Wrz.eps"

set xrange [0:0.3]
set yrange [0:0.2]

set ytics ('0' 0.,0.05,0.1,0.15,0.2)
set format y "%3.2f"
set ylabel "{/Times-Italic z} (cm)" offset 1.5,0.0

unset key
set format cb "%4g"
set cblabel "{/Times-Italic W}({/Times-Italic r},{/Times-Italic z}) (J/m^2)"
set palette defined (0.0 'black', 0.07 'dark-blue', 1.0 'light-blue')

LM = 0.075
RM = 0.300
set lmargin at screen LM 
set rmargin at screen RM

set size 0.4,0.86
set multiplot

set bmargin at screen 0.61
set tmargin at screen 0.83
unset xtics
set label 12 "(a)" at screen 0.,0.83 font "Times-Roman"
p '../Wrz_Gaussian.prof' nonuniform matrix with image

unset label 1
unset label 12

set bmargin at screen 0.33
set tmargin at screen 0.58
set label 22 "(b)" at screen 0.,0.58 font "Times-Roman"
unset xtics
p '../Wrz_FlatTop.prof' nonuniform matrix with image
unset label 22

set bmargin at screen 0.05
set tmargin at screen 0.3
set colorbox 
set label 32 "(c)" at screen 0.0,0.3 font "Times-Roman"
set xtics (0.,0.1,0.2,"{/Times-Italic r} (cm)" 0.3)
p '../Wrz_Donut.prof' nonuniform matrix with image

unset multiplot


