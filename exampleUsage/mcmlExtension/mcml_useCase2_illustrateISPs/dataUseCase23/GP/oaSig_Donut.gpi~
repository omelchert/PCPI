set terminal postscript enhanced eps color lw 1.5 "Times-Roman" 22
set output "./FIGS/mcml_oaSig_donut.eps"

set size 1.,0.74

LW = 1.5
set style line 1 lw LW lt 1 lc rgb '#332288' 
set style line 2 lw LW lt 1 lc rgb '#88CCEE'  
set style line 3 lw LW lt 1 lc rgb '#44AA99' 
set style line 4 lw LW lt 1 lc rgb '#117733' 
set style line 5 lw LW lt 1 lc rgb '#999933' 
set style line 6 lw LW lt 1 lc rgb '#DDCC77' 
set style line 7 lw LW lt 1 lc rgb '#CC6677' 
set style line 8 lw LW lt 1 lc rgb '#882255' 
set style line 9 lw LW lt 1 lc rgb '#AA4499' 

set key samplen 1.0
set key at graph 0.42,0.96
set xzeroaxis

set multiplot

set lmargin at screen 0.1
set rmargin at screen 0.94
set bmargin at screen 0.1
set tmargin at screen 0.7
set xrange [-0.01:0.25]
set yr [-25:45]
set ylabel "{/Times-Italic p} (Pa per J)" offset 1.,0. 
set xtics (0,0.05,0.1,0.15,0.2,'{/Times-Italic c}{/Symbol t} (cm)' 0.25)
p \
"../oaSignal_Donut_zD-0.5.dat"  u 1:3 w l ls 1 t '{/Times-Italic z}_{/=16 D}=-0.5 cm',\
"../oaSignal_Donut_zD-1.0.dat"  u 1:3 w l ls 3 t '-1.0 cm',\
"../oaSignal_Donut_zD-2.0.dat"  u 1:3 w l ls 5 t '-2.0 cm',\
"../oaSignal_Donut_zD-4.0.dat"  u 1:3 w l ls 7 t '-4.0 cm',\
"../oaSignal_Donut_zD-8.0.dat"  u 1:3 w l ls 9 t '-8.0 cm',\


set lmargin at screen 0.53
set rmargin at screen 0.92
set bmargin at screen 0.4
set tmargin at screen 0.68
set xrange [0.19:0.24]
set yr [-0.7:0.2]
unset ylabel
set xtics (0.19,0.2,0.21,0.22,0.23, '{/Times-Italic c}{/Symbol t}' 0.24)
set ytics (-0.6,-0.4,-0.2,0., '{/Times-Italic p}' 0.2)
unset key
p \
"../oaSignal_Donut_zD-0.5.dat"  u 1:3 w l ls 1 t '{/Times-Italic z}_{/=16 D}=-0.5 cm',\
"../oaSignal_Donut_zD-1.0.dat"  u 1:3 w l ls 3 t '-1.0 cm',\
"../oaSignal_Donut_zD-2.0.dat"  u 1:3 w l ls 5 t '-2.0 cm',\
"../oaSignal_Donut_zD-4.0.dat"  u 1:3 w l ls 7 t '-4.0 cm',\
"../oaSignal_Donut_zD-8.0.dat"  u 1:3 w l ls 9 t '-8.0 cm',\


unset multiplot






