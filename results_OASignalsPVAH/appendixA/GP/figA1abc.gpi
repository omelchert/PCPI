#set terminal postscript enhanced eps color lw 1.5 "Times-Roman" 24
set terminal postscript enhanced eps size 9cm,8cm color lw 1 10 font "Times-Roman"
set output "./FIGS/figA1abc.eps"

LW = 1.
set style line 1 lw LW lt 1 lc rgb '#332288'
set style line 2 lw LW lt 1 lc rgb '#88CCEE'
set style line 3 lw LW lt 1 lc rgb '#44AA99'
set style line 4 lw LW lt 1 lc rgb '#117733'
set style line 5 lw LW lt 1 lc rgb '#999933'
set style line 6 lw LW lt 1 lc rgb '#DDCC77'
set style line 7 lw LW lt 1 lc rgb '#CC6677'
set style line 8 lw LW lt 1 lc rgb '#882255'
set style line 9 lw LW lt 1 lc rgb '#AA4499'

LW=1.5
set style line 111 lc rgb 'black' lt 1 lw LW
set style line 112 lc rgb 'dark-grey' lt 1 lw LW

set key samplen 1.0 Right 

set xrange [-0.01:0.13]

set ylabel "{/Times-Italic p} (normalized)" 
set ytics -1.2,0.4,1.2
set format y '' 
set xzeroaxis

set lmargin at screen 0.13
set rmargin at screen 0.9
set multiplot

set tmargin at screen 0.92
set bmargin at screen 0.67
#set xtics ('' -0.05,'' 0, '' 0.025, '' 0.05, '' 0.075, '' 0.1, '' 0.13)
set xtics (-0.05,0,0.025,0.05,0.075,0.1,'{/Times-Italic c}{/Symbol t} (cm)' 0.13)
set yrange [-1.1:1.1]
set label 11 '(a)' at screen 0.01,0.92
set label 12 "{/Symbol l} = 550 nm \n{/Symbol m}_{/Times-Italic=8 a} = 13 cm^{-1}" at graph 0.4,0.9
p "../useCase1/dataUseCase1/L550nm_ma13.0_ftd0.3.dat" u 2:4 w l ls 111 t '{/Times-Italic r}_{/=8 0} = 0.30 cm'\
, "../useCase1/dataUseCase1/L550nm_ma13.0_ftd0.06.dat" u 2:4 w l ls 111 dashtype 2 t '0.06 cm'
unset label 11
unset label 12

set tmargin at screen 0.61
set bmargin at screen 0.36
set xtics (-0.05,0,0.025,0.05,0.075,0.1,'{/Times-Italic c}{/Symbol t} (cm)' 0.13)
set yrange [-0.6:1.1]
set label 21 '(b)' at screen 0.01,0.61
set label 22 "{/Symbol l} = 532 nm \n{/Symbol m}_{/Times-Italic=8 a} = 80 cm^{-1}" at graph 0.4,0.9
p "../useCase1/dataUseCase1/L532nm_ma80.0_ftd0.3.dat" u 2:4 w l ls 111 t '{/Times-Italic r}_{/=8 0} = 0.30 cm'\
, "../useCase1/dataUseCase1/L532nm_ma80.0_ftd0.06.dat" u 2:4 w l ls 111 dashtype 2 t '0.06 cm'
unset label 21
unset label 22


set tmargin at screen 0.30
set bmargin at screen 0.05


set label 31 '(c)' at screen 0.01,0.30
set key samplen 1.
set xzeroaxis
set xr [500:2500]
set yr [-1.7:2.3]
set xtics (500,1000,1500,2000, "{/Times-Italic t} (ns)" 2500)
set ytics (-3,-2,-1,0.,1,2,3)
set ylabel "{/Times-Italic p} (bar)" offset 1,0
set format y "%3.2g"
scalex(x)=x*10**(9)
scaley(y)=y/10**5

p '../useCase2/dataUseCase2/oaSignal_fta0.025.dat' u (scalex($1)):(scaley($3)) w l ls 111 dashtype 2 t '{/Times-Italic r}_{/=8 0} = 0.25 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.05.dat' u (scalex($1)):(scaley($3))  w l ls 112 dashtype 2 t '0.50 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.075.dat' u (scalex($1)):(scaley($3)) w l ls 112 dashtype 3 t '0.75 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.1.dat' u (scalex($1)):(scaley($3))   w l ls 112 dashtype 4 t '1.00 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.194.dat' u (scalex($1)):(scaley($3)) w l ls 111 t '1.94 mm'\

unset label 31

unset multiplot
