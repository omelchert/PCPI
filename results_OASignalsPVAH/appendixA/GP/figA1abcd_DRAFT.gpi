#set terminal postscript enhanced eps color lw 1.5 "Times-Roman" 24
#set terminal postscript enhanced eps size 9cm,10.6cm color lw 1 10 font "Times-Roman"
set terminal postscript enhanced eps size 14cm,16.5cm color lw 1 16 font "Times-Roman"
set output "./FIGS/figA1abcd_DRAFT.eps"

LW=1.5
set style line 111 lc rgb 'black' lt 1 lw LW
set style line 112 lc rgb 'dark-grey' lt 1 lw LW
set label 103 "{/Times-Roman=100 DRAFT}" at screen 0.5,0.15 center rotate by 25 tc "grey" front 
set label 104 "{/Times-Roman=100 DRAFT}" at screen 0.5,0.45 center rotate by 25 tc "grey" front 
set label 105 "{/Times-Roman=100 DRAFT}" at screen 0.5,0.75 center rotate by 25 tc "grey" front 

set key samplen 1.0 Right 

set xrange [-0.01:0.13]

set ylabel "{/Times-Italic p} (normalized)" 
set ytics -1.2,0.4,1.2
set format y '' 
set xzeroaxis


set lmargin at screen 0.13
set rmargin at screen 0.9
set multiplot

set tmargin at screen 0.98
set bmargin at screen 0.79
#set xtics ('' -0.05,'' 0, '' 0.025, '' 0.05, '' 0.075, '' 0.1, '' 0.13)
set xtics (-0.05,0,0.025,0.05,0.075,0.1,'{/Times-Italic c}{/Symbol t} (cm)' 0.13)
set yrange [-1.1:1.1]
set label 11 '(a)' at screen 0.01,0.98
set label 12 "{/Symbol m}_{/Times-Italic=12 a} = 13 cm^{-1}" at graph 0.4,0.9
p "../useCase1/dataUseCase1/L550nm_ma13.0_ftd0.3.dat" u 2:4 w l ls 111 t '{/Times-Italic r}_{/=12 0} = 3.0 mm'\
, "../useCase1/dataUseCase1/L550nm_ma13.0_ftd0.06.dat" u 2:4 w l ls 111 dashtype 2 t '0.6 mm'
unset label 11
unset label 12

set tmargin at screen 0.73
set bmargin at screen 0.54
set xtics (-0.05,0,0.025,0.05,0.075,0.1,'{/Times-Italic c}{/Symbol t} (cm)' 0.13)
set yrange [-0.6:1.1]
set label 21 '(b)' at screen 0.01,0.73
set label 22 "{/Symbol m}_{/Times-Italic=12 a} = 80 cm^{-1}" at graph 0.4,0.9
p "../useCase1/dataUseCase1/L532nm_ma80.0_ftd0.3.dat" u 2:4 w l ls 111 t '{/Times-Italic r}_{/=12 0} = 3.0 mm'\
, "../useCase1/dataUseCase1/L532nm_ma80.0_ftd0.06.dat" u 2:4 w l ls 111 dashtype 2 t '0.6 mm'
unset label 21
unset label 22


set tmargin at screen 0.48
set bmargin at screen 0.29


set label 31 '(c)' at screen 0.01,0.48
set label 32 "{/Symbol m}_{/Times-Italic=12 a} = 24 cm^{-1}" at graph 0.4,0.9
set key samplen 1.
set xzeroaxis
set xr [0.5:2.5]
set yr [-1.7:2.3]
set xtics (0.5,1,1.5,2, "{/Times-Italic t} ({/Symbol m}s)" 2.5)
set ytics (-3,-2,-1,0.,1,2,3)
set ylabel "{/Times-Italic p} (bar)" offset 1,0
set format y "%3.2g"
scalex(x)=x*10**(6)
scaley(y)=y/10**5

p '../useCase2/dataUseCase2/oaSignal_fta0.025.dat' u (scalex($1)):(scaley($3)) w l ls 111 dashtype 2 t '{/Times-Italic r}_{/=12 0} = 0.25 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.05.dat' u (scalex($1)):(scaley($3))  w l ls 112 dashtype 2 t '0.50 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.075.dat' u (scalex($1)):(scaley($3)) w l ls 112 dashtype 3 t '0.75 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.1.dat' u (scalex($1)):(scaley($3))   w l ls 112 dashtype 4 t '1.00 mm'\
, '../useCase2/dataUseCase2/oaSignal_fta0.194.dat' u (scalex($1)):(scaley($3)) w l ls 111 t '1.94 mm'\

unset label 31
unset label 32



set tmargin at screen 0.23
set bmargin at screen 0.04


set label 41 '(d)' at screen 0.01,0.23
set label 42 "{/Symbol m}_{/Times-Italic=12 a} = 16.5 cm^{-1}" at graph 0.4,0.9
set key samplen 1.
set xzeroaxis
set xr [1:4]
set xtics (1,1.5,2,2.5, 3,3.5,4, "{/Times-Italic t} ({/Symbol m}s)" 4)
set yrange [-0.9:1.1]
set ytics -1.2,0.4,1.2
set ylabel "{/Times-Italic p} (normalized)" 
set format y ''
set key invert

p '../useCase3/dataUseCase3/oaSignal_W02.6_R00.5.dat'  u 1:3 w l ls 112 dashtype 4 t '0.50 mm'\
, '../useCase3/dataUseCase3/oaSignal_W02.6_R00.75.dat' u 1:3 w l ls 112 dashtype 3 t '0.75 mm'\
, '../useCase3/dataUseCase3/oaSignal_W02.6_R01.0.dat'  u 1:3 w l ls 112 dashtype 2 t '1.00 mm'\
, '../useCase3/dataUseCase3/oaSignal_W02.6_R01.5.dat'  u 1:3 w l ls 111 dashtype 1 t '{/Times-Italic r}_{/=12 D} = 1.50 mm'\


unset label 41
unset label 42

unset multiplot
